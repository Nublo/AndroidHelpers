import groovy.json.JsonSlurper
import groovy.xml.MarkupBuilder

apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.0"

    defaultConfig {
        applicationId "by.anatoldeveloper.helpers.testframeworks"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def addNodes(MarkupBuilder resources, Object json, String prefix = "") {
    json.each {
        k, v ->
            if (v instanceof String) {
                def append = prefix.length() == 0 ? "" : "${prefix}_"
                resources.string(name: "${append}${k}", "${v}")
            } else {
                def append = prefix.length() == 0 ? "" : "${prefix}_";
                addNodes(resources, v, "${append}${k}")
            }
    }
}

task generateStrings {
    def resources = new File('app/resources.json')
    def json = new JsonSlurper().parseText(resources.text)
    def writer = new StringWriter()
    def xml = new MarkupBuilder(writer)
    xml.setDoubleQuotes(true)

    xml.resources() {
        addNodes(xml, json)
    }

    def stringsFile = new File('app/src/main/res/values/strings.xml')
    stringsFile.write(writer.toString())
}

dependencies {
    compile 'com.android.support:appcompat-v7:27.0.0'
    compile 'com.android.support:cardview-v7:27.0.0'
    compile 'com.android.support:design:27.0.0'
    compile 'com.afollestad.material-dialogs:core:0.9.5.0'

    // Rx
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

    implementation 'android.arch.lifecycle:extensions:1.0.0-rc1'
}